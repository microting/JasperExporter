apply plugin: 'base'
apply plugin: 'idea'
apply plugin: 'build-time-tracker'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.11.+"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
    }
}

buildtimetracker {
    reporters {
        summary {
            ordered false
            threshold 50
            barstyle "unicode"
        }
    }
}

subprojects {
    group "com.microting"
    version "1.1"

    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
            vendor = JvmVendorSpec.ADOPTOPENJDK
        }
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    dependencyManagement {
        imports {
            mavenBom "org.junit:junit-bom:5.8.2"
        }

        dependencies {
            dependency 'net.sf.jasperreports:jasperreports:6.18.1'
            dependency 'commons-io:commons-io:2.11.0'
            dependency 'xalan:xalan:2.7.2'
            dependency 'org.apache.poi:poi:5.2.0'
            dependency 'org.apache.logging.log4j:log4j-1.2-api:2.17.1'
            dependency 'org.apache.logging.log4j:log4j-core:2.17.1'
            dependency 'org.apache.commons:commons-lang3:3.12.0'
        }
    }

    println "moduleName: ${it.name}"
}

wrapper {
    distributionUrl = 'https://services.gradle.org/distributions/gradle-7.3.3-bin.zip'
}